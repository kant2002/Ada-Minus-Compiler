--	TEXT_IO according to LRM
--	j van katwijk
--
with io_exceptions;
with unix;

package text_io is
   type file_type is limited private;
   type file_mode is (in_file, out_file);

   type count is range 0 .. 511;
   subtype positive_count is count range 1 .. count' last;
   unbounded : constant count := 0;

   subtype field	is integer range 0 .. 127;
   subtype number_base  is integer range 2 .. 16;



   --- limited file management

   procedure create (file:	in out file_type;
		     mode:	in file_mode;
		     name:	in string;
		     form:	in string    := " ");

   procedure open   (file:	in out file_type;
		     mode:	in file_mode;
		     name:	in string;
		     form:	in string    := " ");

   procedure close  (file:	in out file_type);
   procedure delete (file:	in out file_type);

   function mode    (file:	in file_type) return file_mode;
   function name    (file:	in file_type) return string;
   function is_open (file:	in file_type) return boolean;

   ---

   procedure set_input (file:	in file_type);
   procedure set_output (file:	in file_type);

   function standard_input return file_type;
   function standard_output return file_type;

   function current_input return file_type;
   function current_output return file_type;
--
--
   procedure set_line_length (file:	in file_type;
			      to:	in count);
   procedure set_line_length (to:	in count);
--
   procedure set_page_length (file:	in file_type;
		              to:	in count);

   procedure set_page_length (to:	in count);
--
   function line_length      (file:	in file_type) return count;
   function line_length   return count;
--
   function page_length      (file:	in file_type) return count;
   function page_length   return count;
--
   procedure new_line (file:	in file_type;
		       spacing:	in positive_count := 1);
   procedure new_line (spacing:	in positive_count := 1);
--
   procedure skip_line (file:	in file_type; spacing : in positive_count := 1);
   procedure skip_line (spacing:	in positive_count := 1);
--
   function end_of_line (file:	in file_type) return boolean;
   function end_of_line return boolean;
--
   procedure new_page    (file:	in file_type);
   procedure new_page;
--
   procedure skip_page (file:	in file_type);
   procedure skip_page;
--
   function end_of_page (file:	in file_type) return boolean;
   function end_of_page return boolean;
--
   function end_of_file (file:	in file_type) return boolean;
   function end_of_file return boolean;
--
   procedure set_col (file:	in file_type; to:	in positive_count);
   procedure set_col (to:	in positive_count);
--
   procedure set_line (file:	in file_type; to:	in positive_count);
   procedure set_line (to:	in positive_count);
--
   function col (file:		in file_type) return positive_count;
   function col return positive_count;
--
   function line (file:		in file_type) return positive_count;
   function line return positive_count;
--
   function page (file:		in file_type) return positive_count;
   function page return positive_count;
--
--
   procedure get (file:	in file_type; item:	out character);
   procedure get (item:	out character);
--
   procedure put (file:		in file_type;
		  item:		in character);
   procedure put (item:		in character);
--
   procedure get (file:	in file_type;	item:	out string);
   procedure get (item:	out string);
--
   procedure put (file:	in file_type;	item:	in string);
   procedure put (item:	in string);
--
   procedure get_line (file:	in file_type;
		       item:	out string;
		       last:	out natural);
   procedure get_line (item:	out string;
		       last:	out natural);
--
   procedure put_line (file:	in file_type;
		       item:	in string);
   procedure put_line (item:	in string);

-- exceptions
   status_error:	exception renames io_exceptions. status_error;
   mode_error:		exception renames io_exceptions. mode_error;
   name_error:		exception renames io_exceptions. name_error;
   use_error:		exception renames io_exceptions. use_error;
   device_error:	exception renames io_exceptions. device_error;
   end_error:		exception renames io_exceptions. end_error;
   data_error:		exception renames io_exceptions. data_error;
   layout_error:	exception renames io_exceptions. layout_error;
--
--the following routines are there for integer_io
--
   function digvalue (x:	long;
		      y:	integer) return character;
   procedure p2 (file:	in file_type;
	         item:	in long;
	         width:	in field;
		 base:	in number_base;
		 neg:	in boolean);
private
   type filedata;
   type file_type is access filedata;
end text_io;

--
--	body of text_io
--
--	j van katwijk
--
with io_exceptions;
package body text_io is
--
--	preliminary implementation of some text io functions
--	J van Katwijk
--
--	This (very preliminary) implementation heavily relies
--	on the buffering mechanism of the standard io library
--
--
--	data structure definitions
--
   subtype filename	is string (1 .. 50);	-- ??

   type file_layout is record
      curr_max:		count;	-- max line size
      curr_max_lines:   count;  -- max lines on page
      curr_pnt:		count;	-- curr character pointer
      curr_line:        count;	-- current line indication
      curr_page:        count;	-- current page indication
   end record;
   
   type layout_type is access file_layout;
       
   type filedata is record
      curr_name:	filename;
      curr_file:	unix. stream;	-- unix file descriptor
      mode:		file_mode;	-- reading/writing
      layout:           layout_type;    -- layout of file
end record;

   MAX_SLOTS    : constant long := 100;
   subtype slots is long range 0 .. MAX_SLOTS;
   type tty_slot is array (slots) of layout_type;
   slot_lijst    : tty_slot ;
-- slot_lijst    : tty_slot := (null, others => null);
   
   std_inp :	file_type;	-- standard input channel
   std_outp:	file_type;	-- standard output channel

   curr_inp:	file_type;	-- current input channel
   curr_outp:	file_type;	-- current output channel

--
-- internally used functions and procedures
--
   function new_file (s: long;	-- unix filedescriptor
		      f: string := " ";
		      m: file_mode) return file_type;

   function digvalue (x:	long;
		      y:	integer) return character is
   begin
	if x < 10
	then
		return character' val (character' pos ('0') + x);
	else
		return character' val (character' pos ('a') + x - 10);
	end if;
   end;

   procedure close   (file:	in out file_type) is
   begin
	if file = null
	then
		raise io_exceptions. status_error;
	end if;

	if (unix. $fclose (file. curr_file) = unix. eof)
	then
		raise io_exceptions. status_error;
	end if;
	$deallocate (file);
   end;

   procedure p2 (file:	in file_type;
		 item:	long;
		 width:	field;
		 base:	number_base;
		 neg:	boolean) is
   begin
	if item < long (base)
	then
		for i in 1 .. width - 1 loop
		   put (file, ' ');
		end loop;
		if neg
		then
		   put (file, '-');
		else
		   put (file, ' ');
		end if;
		put (file, digvalue (item, base));
	else
		p2 (file, item / 10, width - 1, base, neg);
		put (file, digvalue (item rem 10, base));
	end if;
	end;

   procedure delete (file:	in out file_type) is
   begin
	raise io_exceptions. not_yet_implemented;
   end;

--
--	make a string into a c string
--
   function c_name (s:	string) return string is
   t : string (s' first .. s' last + 1);
   begin
	put_line(s);
	put_line(integer'image(s'length));
	t (s' range) := s;
	t (t' last)  := ASCII. NUL;
	return t;
   end;

   procedure create (file:	in out file_type;
		     mode:	in file_mode;
		     name:	in string;
		     form:	in string    := " ") is
   x : long;
   begin
	if (file /= NULL)	-- already existing
	then
		close (file);
	end if;
	if mode = in_file
	then
		x := unix. $fopen (c_name (name), "r");
	else
		x := unix. $fopen (c_name (name), "w");
	end if;
	if (x = unix. eof)
	then
		raise io_exceptions. name_error;
	end if;
	file := new_file (x, name, mode);
   end;

   procedure open (file:	in out file_type;
		   mode:	in file_mode;
		   name:	in string;
		   form:	in string := " ") is
	x : long;
   begin
	if file /= NULL
	then
	   close (file);
	end if;
	create (file, mode, name, form);
   end;

   function mode (file:		in file_type) return file_mode is
   begin
	if (file = NULL)
	then
		raise io_exceptions. mode_error;
	end if;
	return file. mode;
   end;

   function name (file:		in file_type) return string is
   begin
	if file = NULL
	then
		raise io_exceptions. name_error;
        end if;
	return file. curr_name;
   end;

   function is_open (file:	in file_type) return boolean is
   begin
	return file /= NULL;
   end;

   procedure set_input (file:	in file_type) is
   begin
	if file = null
	then
		raise io_exceptions. status_error;
	end if;
	curr_inp := file;
   end;

   procedure set_output (file:	in file_type) is
   begin
	if file = null
	then
		raise io_exceptions. status_error;
	end if;
	curr_outp := file;
   end;

   function standard_input return file_type is
   begin
	return std_inp;
   end;

   function standard_output return file_type is
   begin
	return std_outp;
   end;

   function current_input return file_type is
   begin
	return curr_inp;
   end;

   function current_output return file_type is
   begin
	return curr_outp;
   end;

   procedure set_line_length (file:	in file_type;
			      to:	in count) is
   begin
	if (file = NULL)
	then
		raise io_exceptions. status_error;
	end if;
	if file. mode = out_file
	then
		if file. layout. curr_pnt > to
		then
			new_line (file);
		end if;	
		file. layout. curr_max := to;
	end if;
   end;

   procedure set_line_length (to:	in count) is
   begin
	set_line_length (curr_outp, to);
   end;

   procedure set_page_length (file:	in file_type;
		              to:	in count) is
   begin
	if (file = NULL)
	then
		raise io_exceptions. status_error;
	end if;
	if file. mode = out_file
	then
		if file. layout. curr_line > to
		then
			new_page (file);
		end if;	
		file. layout. curr_max_lines := to;
	end if;
   end;

   procedure set_page_length (to:	in count) is
   begin
      set_page_length (curr_outp, to);
   end;

   function line_length (file:	in file_type) return count is
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= out_file
	then
		raise mode_error;
	end if;
	return count (file. layout. curr_max);
   end;

   function line_length return count is
   begin
	return line_length (curr_outp);
   end;

   function page_length (file:	in file_type) return count is
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= out_file
	then
		raise mode_error;
	end if;
	return count (file. layout. curr_max_lines);
   end;

   function page_length return count is
   begin
	return page_length (curr_outp);
   end;

   procedure new_line (file:	in file_type;
		       spacing:	in positive_count := 1) is
   begin
	if file = null
	then
		raise status_error;
	end if;
	if file. mode /= out_file
	then
		raise mode_error;
	end if;
	for i in 1 .. spacing loop
	   file. layout. curr_pnt := 1;
           unix. $fputc (ASCII. LF, file. curr_file);
           if file. layout. curr_line >= file. layout. curr_max_lines
           then
               new_page;
           else
               file. layout. curr_line := file. layout. curr_line + 1;
           end if;
	end loop;
   end;

   procedure new_line (spacing:	in positive_count := 1) is
   begin
	new_line (curr_outp, spacing);
   end;

   procedure skip_line (file:	in file_type;
		        spacing:	in positive_count := 1) is
   c: character;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;

	for i in 1 .. spacing loop
		while not end_of_line (file) loop
		   get (file, c);
		end loop;
                if file. layout. curr_line >= file. layout. curr_max_lines
                then
                    new_page;
                else
                    file. layout. curr_line := file. layout. curr_line + 1;
		    file. layout. curr_pnt := 1;
                end if;
	end loop;
   end;

   procedure skip_line (spacing:	in positive_count := 1) is
   begin
      skip_line (curr_inp, spacing);
   end;

   function end_of_line (file:	in file_type) return boolean is
	c : long;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;
	c := unix. $fgetc (file. curr_file);
        unix. $ungetc (c, file. curr_file);
	return c = unix.dlf;
   end;

   function end_of_line return boolean is
   begin
	return end_of_line (curr_inp);
   end;

   procedure new_page (file:	in file_type) is
   begin
	if file = null
	then
		raise status_error;
	end if;
	if file. mode /= out_file
	then
		raise mode_error;
	end if;
        file. layout. curr_pnt := 1;
        file. layout. curr_line := 1;
        file. layout. curr_page := file. layout. curr_page + 1;
        unix. $fputc (ASCII. FF, file. curr_file);
   end;

   procedure new_page is
   begin
	new_page (curr_outp);
   end;

   procedure skip_page (file:	in file_type) is
   c: character;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;

	while not end_of_page (file) loop
	   get (file, c);
	end loop;
	file. layout. curr_page := file. layout. curr_page + 1;
	file. layout. curr_line := 1;
	file. layout. curr_pnt := 1;
   end;

   procedure skip_page is
   begin
	skip_page (curr_inp);
   end;

   function end_of_page (file:	in file_type) return boolean is
        c : long;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;
	c := unix. $fgetc (file. curr_file);
        unix. $ungetc (c, file. curr_file);
	return c = unix.dff;
   end;

   function end_of_page return boolean is
   begin
	return end_of_page (curr_inp);
   end;

   function end_of_file (file:	in file_type) return boolean is
	c : long;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;
	c := unix. $fgetc (file. curr_file);
	unix. $ungetc (c, file. curr_file);

	return c = unix. eof;
   end;

   function end_of_file return boolean is
   begin
	return end_of_file (curr_inp);
   end;

   procedure set_col (file:	in file_type;
		      to:	positive_count) is
	c : character;
   begin
	if file = null
	then
		raise status_error;
	end if;

	if file. mode = out_file
	then
	   if to < file. layout. curr_pnt
	   then
	      new_line;
	   end if;
	   for i in file. layout. curr_pnt .. to - 1 loop
	      put (file, ' ');
	   end loop;	-- get and put take care of setting layout. curr_pnt
	else
	   for i in file. layout. curr_pnt .. to - 1 loop
	      get (file, c);
	   end loop;
	end if;
   end;

   procedure set_col (to:	positive_count) is
   begin
	set_col (curr_outp, to);
   end;

   procedure set_line (file:	in file_type; to:	in positive_count) is
   begin
	raise io_exceptions. not_yet_implemented;
   end;

   procedure set_line (to:	in positive_count) is
   begin
	set_line (curr_inp, to);
   end;

   function col (file:		in file_type) return positive_count is
   begin
	if file = null
	then
		raise status_error;
	end if;
	return positive_count (file. layout. curr_pnt);
   end;

   function col return positive_count is
   begin
	return col (curr_outp);
   end;

   function line (file:	in file_type) return positive_count is
   begin
	if file = null
	then
		raise status_error;
	end if;
	return file. layout. curr_line;
   end;

   function line return positive_count is
   begin
	return line (curr_outp);
   end;

   function page (file:	in file_type) return positive_count is
   begin
	if file = null
	then
		raise status_error;
	end if;
	return file. layout. curr_page;
   end;

   function page return positive_count is
   begin
	return page (curr_outp);
   end;

   procedure get (file:	in file_type; item:	out character) is
	d : long;
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= in_file
	then
		raise mode_error;
	end if;
	-- leave reading characters to unix
	--
	d := unix. $fgetc (file. curr_file);
	if d = unix. eof
	then
		raise end_error;
	end if;
	if d = unix. dlf	-- linefeed
	then
                if file. layout. curr_line >= file. layout. curr_max_lines
                then
                    new_page;
                else
		    file. layout. curr_line := file. layout. curr_line + 1;
		    file. layout. curr_pnt  := 1;
                end if;
		get (file, item);
-- no pascal here          item := ' ';
		return;
	end if;
	file. layout. curr_pnt := file. layout. curr_pnt + 1;
	item := character 'val (d);
   end;

   procedure get (item:	out character) is
   begin
	get (curr_inp, item);
   end;

   procedure put (file:	in file_type; item:	in character) is
   begin
	if file = null
	then
		raise status_error;
	elsif file. mode /= out_file
	then
		raise mode_error;
	end if;
	unix. $fputc (item, file. curr_file);
	if item = ASCII. LF
	then
                if file. layout. curr_line >= file. layout. curr_max_lines
                then
                    new_page;
                else
		    file. layout. curr_pnt := 1;
		    file. layout. curr_line := file. layout. curr_line + 1;
                end if;
	elsif file. layout. curr_pnt >= file. layout. curr_max
	then
		new_line (file);
	else
		file. layout. curr_pnt := file. layout. curr_pnt + 1;
	end if;
   end;

   procedure put (item:	in character) is
   begin
	put (curr_outp, item);
   end;

   procedure get (file:	in file_type;
		  item:	out string) is
   begin
	for i in item' range loop
		get (file, item (i));
	end loop;
   end;

   procedure get (item:	out string) is
   begin
	for i in item' range loop
		get (curr_inp, item (i));
	end loop;
   end;

   procedure put (file:	in file_type;
		  item:	in string) is
   begin
	for i in item' range loop
		put (file, item (i));
	end loop;
   end;

   procedure put (item:	in string) is
   begin
	for i in item' range loop
		put (curr_outp, item (i));
	end loop;
   end;

   procedure get_line (file:	in file_type;
		       item:	out string;
		       last:	out natural) is
   c : character;
   begin
	for i in item' range loop
	   get (file, c);
	   item (i) := c;
	   last := i;
	   exit when end_of_line (file);
	end loop;
   end;

   procedure get_line (item:	out string;
		       last:	out natural) is
   begin
	get_line (curr_inp, item, last);
   end;

   procedure put_line (file:	in file_type;
		       item:	in string) is
   begin
	put (file, item);
	new_line (file);
   end;

   procedure put_line (item:	in string) is
   begin
	put_line (curr_outp, item);
   end;

   function new_file (s:	long;	-- unix filedescriptor
		      f:	string;
		      m:	file_mode) return file_type is
   slot : long;
   t : file_type;
   begin
	t := new filedata;
	t. mode		:= m;
	t. curr_name (1 .. f' last) := f;
	t. curr_name (f' last + 1) := ASCII. NUL;
	t. curr_file	:= s;
        --
        -- a terminal can be both input and output with different
        -- file descriptors, because of layout management they should have
        -- the same layout descriptor.
        -- ttyslot returns for a terminal a positive unique number
        -- which is used as an index in an array
        --
        slot := unix. $ttyslot(s);
	if slot > 0
	then
	    if slot_lijst (slot) = null
	    then
	        slot_lijst (slot) := new file_layout;
                t. layout := slot_lijst (slot);
	    else
                t. layout := slot_lijst (slot);
	        return t;
            end if;
	else
            t. layout := new file_layout;
        end if;
--	if m = in_file
--	then
--		t. layout. curr_max := 0;	-- force reading
--	else
		t. layout. curr_max := 80;
--	end if;
        t. layout. curr_max_lines := 60;
	t. layout. curr_pnt	:= 1;
	t. layout. curr_line	:= 1;
	t. layout. curr_page	:= 1;

	return t;
   exception
	when constraint_error =>	-- filename does not fit
			t. curr_name := f (1 .. t. curr_name' last - 1);
			t. curr_name (t. curr_name' last) := ASCII. NUL;
   end;
begin
	std_inp:= new_file (unix. $termin, "standard_input", in_file);
	std_outp:= new_file (unix. $termout, "standard_output", out_file);
	curr_inp := std_inp;
	curr_outp := std_outp;
end;

